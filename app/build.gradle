plugins {
    id 'org.jetbrains.kotlin.multiplatform'
    id 'com.android.application'
    id 'kotlin-android-extensions'
    id 'kotlinx-serialization'
}
repositories {
    google()
    jcenter()
    mavenCentral()
}

//apply plugin: 'com.android.application'
//apply plugin: 'kotlin-android-extensions'
//apply plugin: 'kotlinx-serialization'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId 'org.jetbrains.kotlin.mpp_app_android'
        minSdkVersion 15
        targetSdkVersion 29
        versionCode 1
        versionName '1.0'
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }

    packagingOptions {
        exclude 'META-INF/*.kotlin_module'
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    //implementation "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVer"
    implementation "org.jetbrains.kotlin:kotlin-serialization:$kotlinVer"
}

kotlin {
    android("android")
    // This is for iPhone emulator
    // Switch here to iosArm64 (or iosArm32) to build library for iPhone device
    iosX64("ios") {
        binaries {
            framework()
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation("io.ktor:ktor-client-core:$rootProject.ktorVer")
                implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$rootProject.coroutinesVer")
                implementation("org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:$rootProject.kotlinSerializationVer")
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        androidMain {
            dependencies {
                implementation kotlin('stdlib')
                implementation("io.ktor:ktor-client-android:$rootProject.ktorVer")
                implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:$rootProject.coroutinesVer")
                implementation("org.jetbrains.kotlinx:kotlinx-serialization-runtime:$rootProject.kotlinSerializationVer")
            }
        }
        androidTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }
        iosMain {
            dependencies {
                implementation("io.ktor:ktor-client-ios:$ktorVer")
                implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core-native:$rootProject.coroutinesVer")
                implementation("org.jetbrains.kotlinx:kotlinx-serialization-runtime-native:$rootProject.kotlinSerializationVer")
            }
        }
        iosTest {
        }
    }
}

// This task attaches native framework built from ios module to Xcode project
// (see iosApp directory). Don't run this task directly,
// Xcode runs this task itself during its build process.
// Before opening the project from iosApp directory in Xcode,
// make sure all Gradle infrastructure exists (gradle.wrapper, gradlew).
task copyFramework {
    def buildType = project.findProperty('kotlin.build.type') ?: 'DEBUG'
    def target = project.findProperty('kotlin.target') ?: 'ios'
    dependsOn kotlin.targets."$target".binaries.getFramework(buildType).linkTask

    doLast {
        def srcFile = kotlin.targets."$target".binaries.getFramework(buildType).outputFile
        def targetDir = getProperty('configuration.build.dir')
        copy {
            from srcFile.parent
            into targetDir
            include 'app.framework/**'
            include 'app.framework.dSYM'
        }
    }
}